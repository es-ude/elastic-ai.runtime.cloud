plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'

    id 'jacoco'
    id 'nebula.facet' version '9.6.3'
    id 'com.adarshr.test-logger' version '4.0.0'
}

sourceCompatibility = '17'

group 'de.ude.ies.elastic_ai.cloud'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.hivemq:hivemq-mqtt-client:1.3.0'
    implementation 'net.sourceforge.argparse4j:argparse4j:0.9.0'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.testcontainers:testcontainers:1.18.3'
    testImplementation 'org.testcontainers:junit-jupiter:1.18.3'
    testImplementation 'org.testcontainers:hivemq:1.18.3'
    testImplementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.3'
}

facets {
    integrationTest {
        parentSourceSet = 'test'
        testTaskName = 'integrationTest'
    }
}

integrationTest {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    // tests are required to run before generating the report
    dependsOn test

    // change output directory of jacoco report
    reports {
        html.outputLocation = layout.projectDirectory.dir("jacocoReport")
    }
}

clean {
    // update clean task to delete old jacoco reports
    delete "${projectDir}/jacocoReport"
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/es-ude/elastic-ai.runtime.cloud")
            credentials {
                username = System.getenv("username")
                password = System.getenv("password")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}